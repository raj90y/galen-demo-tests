@import rules.gspec
@import galen-extras/galen-extras-rules.gspec

@objects
    login_box           .content-area
    login_button        input[value="Login"]
    enroll_button       button[class*="register-button"]
    username_textfield  input[id*="username"]
    password_textfield  input[id*="password"]
    logo                .navbar-brand>img

@groups
    (login_box_button, login_box_buttons)                   login_button
    (login_box_textfield, login_box_textfields)             username_textfield, password_textfield
    (login_box_form_element, login_box_form_elements)       &login_box_textfields
    (login_box_element, login_box_elements)                 &login_box_form_elements, &login_box_buttons

@set
    login_box_elements_vertical_margin  25 to 70px
    login_box_elements_side_margin      ~ 45px
    login_box_desktop_width             400px
    login_button_text                   Log in
    form_textfield_height               45 to 60 px

@on mobile
    @set login_box_elements_side_margin 15 to 110px


= Login box =

    | &login_box_elements should be more or less readable
    | every &login_box_elements has height ${form_textfield_height}
    | &login_box_buttons should be tapable

    = Styling =
        | login_button should look like a primary button

    @on desktop, tablet
        | &login_box_form_elements are aligned vertically above each other with ${login_box_elements_vertical_margin} margin
        | first &login_box_form_element is inside login_box ${login_box_elements_side_margin} top
        | last &login_box_form_element is above login_button ${login_box_elements_vertical_margin}
        | &login_box_buttons are aligned horizontally next to each other with 0 to 5px margin
    @on mobile
        | &login_box_elements are aligned vertically above each other with ${login_box_elements_vertical_margin} margin
        | &login_box_elements sides are vertically inside login_box with ${login_box_elements_side_margin} margin

    login_button:
        text is "${login_button_text}"

    username_textfield:
        aligned vertically left login_button
        aligned vertically left password_textfield

    = Logo =
    logo:
     @on desktop
        image file images/logo-desktop.png

